allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    group = 'org.heat'
    version = '0.6-SNAPSHOT'

	apply from: rootDir.absolutePath + "/gradle/remote.gradle"

    ext {
        config = '1.2.1'
        guava = '17.0'
        fungsi = '1.2.+'
        lombok = '1.14.0'
        hamcrest = '1.3'
        junit = '4.11'
        mockito = '1.9.5'
        guice = '3.0'
    }

    dependencies {
        testCompile "junit:junit:$junit"
        testCompile "org.mockito:mockito-core:$mockito"
    }

    uploadArchives {
        repositories.mavenDeployer {
            if (project != rootProject) {
                pom.artifactId = "util-${project.name}"
            }
        }
    }

    install {
        repositories.mavenInstaller {
            if (project != rootProject) {
                pom.artifactId = "util-${project.name}"
            }
        }
    }
}

configurations {
    provided
}

dependencies {
    compile "com.typesafe:config:$config"
    compile "com.google.guava:guava:$guava"
    compile "org.fungsi:fungsi-core:$fungsi"

    provided "org.projectlombok:lombok:$lombok"
}

sourceSets {
    main { compileClasspath += configurations.provided }
    test { compileClasspath += configurations.provided }
}

idea { module { scopes.PROVIDED.plus += configurations.provided } }

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

task docs(type: Javadoc) {
    source         = files(allprojects*.sourceSets*.main*.allJava)
    classpath      = files(allprojects*.sourceSets*.main*.compileClasspath)
    destinationDir = file("${buildDir}/docs/docs")
}
